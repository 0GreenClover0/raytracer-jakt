import math

struct Vec3 {
    public x: f32
    public y: f32
    public z: f32

    public fn create(anon x: f32 = 0.0, anon y: f32 = 0.0, anon z: f32 = 0.0) -> Vec3 {
        return Vec3(x, y, z)
    }

    public fn create() -> Vec3 {
        return Vec3(x: 0.0f32, y: 0.0f32, z: 0.0f32)
    }

    public fn negate(anon rhs: Vec3) -> Vec3 {
        return Vec3::create(-rhs.x, -rhs.y, -rhs.z)
    }

    public fn length(anon rhs: Vec3) -> f32 {
        return math::AK::sqrt(length_squared(rhs))
    }

    public fn length_squared(anon rhs: Vec3) -> f32 {
        return rhs.x * rhs.x + rhs.y * rhs.y + rhs.z * rhs.z
    }

    public fn dot(anon lhs: Vec3, anon rhs: Vec3) -> f32 {
        return lhs.x * rhs.x + lhs.y * rhs.y + lhs.z * rhs.z
    }

    public fn cross(anon lhs: Vec3, anon rhs: Vec3) -> Vec3 {
        return Vec3::create(
            x: lhs.y * rhs.z - lhs.z * rhs.y,
            y: lhs.z * rhs.x - lhs.x * rhs.z,
            z: lhs.x * rhs.y - lhs.y * rhs.x
        )
    }

    public fn normalize(anon rhs: Vec3) -> Vec3 {
        // FIXME: rhs / length(rhs), but this breaks typechecker
        let length = math::AK::sqrt(rhs.x * rhs.x + rhs.y * rhs.y + rhs.z * rhs.z)
        return Vec3::create(rhs.x / length, rhs.y / length, rhs.z / length)
    }

    public fn random() -> Vec3 {
        return Vec3::create(random_float(), random_float(), random_float())
    }

    public fn random(min: f32, max: f32) -> Vec3 {
        return Vec3::create(random_float(min, max), random_float(min, max), random_float(min, max))
    }

    public fn random_in_unit_sphere() -> Vec3 {
        loop {
            let p = random(min: -1.0f32, max: 1.0f32)
            if Vec3::length_squared(p) < 1.0f32 {
                return p
            }
        }
    }

    public fn random_unit_vector() -> Vec3 {
        return normalize(random_in_unit_sphere())
    }

    public fn random_on_hemisphere(normal: &Vec3) -> Vec3 {
        let on_unit_sphere = random_unit_vector()
        if dot(on_unit_sphere, *normal) > 0.0f32 {
            return on_unit_sphere
        } else {
            return negate(on_unit_sphere)
        }
    }

    public fn to_string(anon rhs: Vec3) -> String {
        return format("{} {} {}", rhs.x, rhs.y, rhs.z)
    }
}

// Vec3 operators

type Vec3 implements(Add<Vec3, Vec3>) {
    fn add(this, anon rhs: Vec3) -> Vec3 {
        return Vec3::create(.x + rhs.x, .y + rhs.y, .z + rhs.z)
    }
}

type Vec3 implements(Subtract<Vec3, Vec3>) {
    fn subtract(this, anon rhs: Vec3) -> Vec3 {
        return Vec3::create(.x - rhs.x, .y - rhs.y, .z - rhs.z)
    }
}

type Vec3 implements(Multiply<Vec3, Vec3>) {
    fn multiply(this, anon rhs: Vec3) -> Vec3 {
        return Vec3::create(.x * rhs.x, .y * rhs.y, .z * rhs.z)
    }
}

type Vec3 implements(AddAssign<Vec3>) {
    fn add_assign(mut this, anon rhs: Vec3) {
        .x += rhs.x
        .y += rhs.y
        .z += rhs.z
    }
}

type Vec3 implements(SubtractAssign<Vec3>) {
    fn subtract_assign(mut this, anon rhs: Vec3) {
        .x -= rhs.x
        .y -= rhs.y
        .z -= rhs.z
    }
}

// float operators

type Vec3 implements(Multiply<f32, Vec3>) {
    fn multiply(this, anon rhs: f32) -> Vec3 {
        return Vec3::create(.x * rhs, .y * rhs, .z * rhs)
    }
}

type f32 implements(Multiply<Vec3, Vec3>) {
    [[inline(always)]]
    fn multiply(this, anon rhs: Vec3) -> Vec3 {
        return rhs * this
    }
}

type Vec3 implements(Divide<f32, Vec3>) {
    fn divide(this, anon rhs: f32) -> Vec3 {
        return (1.0f32 / rhs) * this
    }
}

type Vec3 implements(MultiplyAssign<f32>) {
    fn multiply_assign(mut this, anon rhs: f32) {
        .x *= rhs
        .y *= rhs
        .z *= rhs
    }
}

type Vec3 implements(DivideAssign<f32>) {
    fn divide_assign(mut this, anon rhs: f32) {
        this *= 1f32 / rhs
    }
}
